MACHINE Parking(capacity)
CONSTRAINTS capacity ∈ NAT1
VARIABLES parkingState, cars
SEES ParkingST

// Invariant definition
INVARIANT
// The parking lot should only be in two states
    parkingState ∈ PARKING_STATE ∧
// The cars collection belongs to the boundary set of natural numbers
    cars ∈ FIN(NAT) ∧
// The number of cars in the parking lot cannot exceed its capacity.
    card(cars) ≤ capacity ∧
// When the car park is closed, there should be no cars present.
    (parkingState = closed ⇒ card(cars) = 0)

// Initial state
INITIALISATION
    cars := ∅ ||
    parkingState := closed
OPERATIONS

    // Definition for opening the parking
    openParking =
        BEGIN
            parkingState := opened
        END;

    // Definition for closing the parking
    ok ← closeParking =
        PRE
            card(cars) = 0
        THEN
            parkingState := closed ||
            ok := TRUE
        END;

    // Definition for car arrival
    ok ← arrival(autoId) =
        PRE
            autoId ∈ NAT ∧
            autoId ∉ cars ∧
            parkingState = opened ∧
            card(cars) < capacity
        THEN
            cars := cars ∪ {autoId} ||
            ok := TRUE
        END;

    // Definition for car departure
    ok ← departure(autoId) =
        PRE
            autoId ∈ NAT ∧
            autoId ∈ cars ∧
            parkingState = opened
        THEN
            cars := cars - {autoId} ||
            ok := TRUE
        END
END

//This is separate machine for defining parking state
MACHINE
    ParkingST
SETS PARKING_STATE = { opened, closed }
END